plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id 'org.asciidoctor.jvm.gems' version '3.3.2'
    id 'com.stehno.gradle.webpreview' version '0.3.0'
}

group = 'io.github.cjstehno.envoy'
version = '0.0.1'

repositories {
    mavenCentral()
}

asciidoctor {
    baseDir file('src/docs/asciidoc')
    options doctype: 'book'
    attributes(
        'source-highlighter': 'rouge',
        'coderay-linenums-mode': 'table',
        icon: 'font',
        linkattrs: true,
        encoding: 'utf-8'
    )
}

webPreview {
    resourceDir = file('build/site')
}

task site(dependsOn: ['build', 'envoy-core:javadoc', 'envoy-core:jacocoTestReport', 'envoy-jdk:javadoc', 'envoy-jdk:jacocoTestReport',  'asciidoctor']) {
    doLast {
        def vars = [
            project_version: project.version,
            year           : Calendar.instance.get(Calendar.YEAR)
        ]

        mkdir 'build/site'

        copy {
            from 'src/site'
            into 'build/site'
            include '**/*.html'
            expand vars
        }

        copy {
            from 'src/site'
            into 'build/site'
            include '**/css/**'
            include '**/js/**'
            include '**/img/**'
        }

        // Copy the envoy coverage reports
        copy {
            from 'envoy-core/build/reports/jacoco/test/html'
            include '**/**'
            into 'build/site/envoy-core/jacoco'
        }

        // Copy the envoy-core test reports
        copy {
            from 'envoy-core/build/reports/tests/test'
            include '**/**'
            into 'build/site/envoy-core/tests'
        }

        // Copy the envoy-core javadocs
        copy {
            from 'envoy-core/build/docs/javadoc'
            include '**/**'
            into 'build/site/envoy-core/javadoc'
        }

        // Copy the envoy-jdk coverage reports
        copy {
            from 'envoy-jdk/build/reports/jacoco/test/html'
            include '**/**'
            into 'build/site/envoy-jdk/jacoco'
        }

        // Copy the envoy-jdk test reports
        copy {
            from 'envoy-jdk/build/reports/tests/test'
            include '**/**'
            into 'build/site/envoy-jdk/tests'
        }

        // Copy the envoy-jdk javadocs
        copy {
            from 'envoy-jdk/build/docs/javadoc'
            include '**/**'
            into 'build/site/envoy-jdk/javadoc'
        }

        // copy the generated user-guide
        copy {
            from 'build/docs/asciidoc'
            include 'user_guide.html'
            into 'build/site/docs'
        }
    }
}

task publishSite(type: GradleBuild, group: 'Publishing', description: 'Publishes the documentation web site.', dependsOn: ['site']) {
    buildFile = 'publish.gradle'
    tasks = ['gitPublishPush']
}
